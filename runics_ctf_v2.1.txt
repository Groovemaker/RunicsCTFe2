@name Runics Capture the Minge

# Numbers
@persist [FlagRange FlagScoreAdd OldScore ScoreLimit Marker FlagMarker ShowMarker ShowFlagMarker Game FirstRun TeamHaver LastTeamHaver FFA TDM PERFER]

# Arrays
@persist [RawFlagPoints FlagTitles FlagPoints Team1M Team2M Team1Ent Team2Ent TeamNames TeamColors Team1HUD Team2HUD]:array

# Ents
@persist [FlagHaver LastFlagHaver]:entity

# Vectors
@persist [LastPoint FlagPoint Goal GoalColor FlagColor]:vector 

# Strings
@persist [FlagTitle FlagModel PickSound GoalTitle WinSound DropSound  Team1 Team2 Separator]:string

# Tables
@persist [Score TeamScore TeamMembers]:table

#########################
#   Made by Runic       #
#      27.07.21         #
#   Zero Hour: 10:30    #
#########################


# Hooks for Spawning and Dying
runOnDeath(1)
runOnSpawn(1)
runOnChat(1)

# When Spawned
if(first() || dupefinished()){
    
    # Gamemode IDs, better leave them as is
    FFA = 1
    TDM = 2
    
    # Flag Points and Goals 
       
    ## Bigcity
    if(map() == "gm_bigcity"){
        RawFlagPoints:pushString("-92.285782 -2167.674805 -11071.968750")        # Spawn Area 1
        RawFlagPoints:pushString("-4123.109375 -6056.664551 -11079.968750")      # Crossing 1
        RawFlagPoints:pushString("-6678.682617 -7705.910645 -11071.471680")      # Garage Area 1
        RawFlagPoints:pushString("-9169.187500 1414.988403 -11071.968750")      # Concrete Area 1
        RawFlagPoints:pushString("-908.430664 2561.240723 -9279.968750")         # Two Room Apartment
        RawFlagPoints:pushString("-1236.816040 9895.414063 -11075.822266")       # Garbage Dump
        FlagTitles:pushString("Spawn")
        FlagTitles:pushString("Crossing")
        FlagTitles:pushString("Garage Area")
        FlagTitles:pushString("Concrete Area")
        FlagTitles:pushString("Two Room Apartment Building")
        FlagTitles:pushString("Garbage Dump")
    }
    if(map() == "gm_bigcity_improved"){
        RawFlagPoints:pushString("-92.285782 -2167.674805 -11071.968750")        # Spawn Area 1
        RawFlagPoints:pushString("-4123.109375 -6056.664551 -11079.968750")      # Crossing 1
        RawFlagPoints:pushString("-6678.682617 -7705.910645 -11071.471680")      # Garage Area 1
        RawFlagPoints:pushString("-9169.187500 1414.988403 -11071.968750")      # Concrete Area 1
        RawFlagPoints:pushString("-908.430664 2561.240723 -9279.968750")         # Two Room Apartment
        RawFlagPoints:pushString("-1236.816040 9895.414063 -11075.822266")       # Garbage Dump
        FlagTitles:pushString("Spawn")
        FlagTitles:pushString("Crossing")
        FlagTitles:pushString("Garage Area")
        FlagTitles:pushString("Concrete Area")
        FlagTitles:pushString("Two Room Apartment Building")
        FlagTitles:pushString("Garbage Dump")
    }
    if(map() == "gm_bigcity_improved_lite"){
        RawFlagPoints:pushString("-92.285782 -2167.674805 -11071.968750")        # Spawn Area 1
        RawFlagPoints:pushString("-4123.109375 -6056.664551 -11079.968750")      # Crossing 1
        RawFlagPoints:pushString("-6678.682617 -7705.910645 -11071.471680")      # Garage Area 1
        RawFlagPoints:pushString("-9169.187500 1414.988403 -11071.968750")      # Concrete Area 1
        RawFlagPoints:pushString("-908.430664 2561.240723 -9279.968750")         # Two Room Apartment
        RawFlagPoints:pushString("-1236.816040 9895.414063 -11075.822266")       # Garbage Dump
        FlagTitles:pushString("Spawn")
        FlagTitles:pushString("Crossing")
        FlagTitles:pushString("Garage Area")
        FlagTitles:pushString("Concrete Area")
        FlagTitles:pushString("Two Room Apartment Building")
        FlagTitles:pushString("Garbage Dump")
    }  
    # If there are no documented flag points on the map, exit
    if(RawFlagPoints:count() < 1){
        error("This Map hasn't been charted yet.")
    }
        
    # Documentation:
    
    # To get a position for the game, put "getpos" in console and push that string into the points array 
    # with an if statement of the current map, example down there.
    # IMPORTANT: THE TITLE ARRAY MATCHES UP IN INDICES WITH THE POINTS ARRAY!
    

    
    #################################################################################################################################################################
    #################################################### GAMEMODE CONFIG ############################################################################################
    ##################################### Edit your Settings here, its like a server.cfg in Source Engine. ##########################################################
    #################################################################################################################################################################
    
    Game = FFA                                          # FFA or Team Deathmatch?                   <1=FFA 2=TDM>   # DEFAULT: 1 - Free For All
    ScoreLimit = 5                                      # How much Score til the game is won        <Number>        # DEFAULT: 10
    
    FlagModel = "models/kleiner.mdl"                    # Which model is the flag?                  <Model Path>    # DEFAULT: "models/kleiner.mdl"
    FlagRange = 200                                     # Pickup Range for the Flag                 <Number>        # DEFAULT: 200
    
    PickSound = "items/battery_pickup.wav"              # Pickup Flag Sound                         <Sound Path>    # DEFAULT: "items/battery_pickup.wav"
    DropSound = "vo/k_lab/kl_ahhhh.wav"                 # Drop Flag Sound                           <Sound Path>    # DEFAULT: "vo/k_lab/kl_ahhhh.wav"
    WinSound = "vo/k_lab/kl_excellent.wav"              # Put Flag to Goal Sound                    <Sound Path>    # DEFAULT: "vo/k_lab/kl_excellent.wav"
    
    ShowMarker = 1                                      # Show Goal Markers?                        <1=On 0=Off>    # DEFAULT: 1
    ShowFlagMarker = 1                                  # Show Flag Markers when not picked up?     <1=On 0=Off>    # DEFAULT: 1      
    
    Team1 = "Minges"                                    # Team 1 Name                               <string>        # DEFAULT: "Minges"
    Team2 = "Cops"                                      # Team 2 Name                               <string>        # DEFAULT: "Builders"
    
    FlagColor = vec(255,200,11)                         # Flag Color                                <vector>        # DEFAULT: vec(255,200,11)
    GoalColor = vec(210,255,11)                         # Goal Color                                <vector>        # DEFAULT: vec(210,255,11)
    
    TeamColors[1,vector] = vec(55,130,255)              # Team1 Color                               <vector>        # DEFAULT: vec(55,130,255)
    TeamColors[2,vector] = vec(255,77,11)               # Team2 Color                               <vector>        # DEFAULT: vec(255,77,11)
    
    ################################################################################################################################################################
    
    # Init Variables
    # Hardcoded Vars | DO NOT CHANGE!
    Marker = 33
    FlagMarker = 34
    FirstRun = 1
    TeamNames[1,string] = Team1
    TeamNames[2,string] = Team2
    Separator = toUnicodeChar(9612)
   
    # Function Declarations

    ## Team Balancer
    function balance(){
        Plys = players()
        State = randint(1,0)
        foreach(K,V:entity = Plys){
            if(State){
                TeamMembers[V:steamID(),number] = 1
                Team1M:pushString(V:name())
                Team1Ent:pushEntity(V)
            }else{
                TeamMembers[V:steamID(),number] = 2
                Team2M:pushString(V:name())
                Team2Ent:pushEntity(V)
            }
            State = !State
        }
    }
    ## 3D HUD
    function inithud(){
        foreach(K,V:entity = Team1Ent){  
            Index = K+40
            Team1HUD[K, entity] = V
            holoCreate(Index)
            holoVisible(Index,Team2Ent,0)
            holoVisible(Index,Team1Ent,1)
            holoScale(Index,vec(3,3,V:height()*0.1))
            holoMaterial(Index,"hlmv/debugmrmwireframe")
            holoColor(Index,TeamColors[1,vector])   
        }
        foreach(K,V:entity = Team2Ent){
            Index = K+120
            Team2HUD[K, entity] = V
            holoCreate(Index)
            holoVisible(Index,Team1Ent,0)
            holoVisible(Index,Team2Ent,1)
            holoScale(Index,vec(3,3,V:height()*0.1))
            holoMaterial(Index,"hlmv/debugmrmwireframe")
            holoColor(Index,TeamColors[2,vector])   
        }
    }
    function rebalance(){
        Plys = players()
        State = !State
        TeamMembers = table()
        Team1M = array()
        Team1Ent = array()
        Team2M = array()
        Team2Ent = array()
        foreach(K,V:entity = Plys){
            if(State){
                TeamMembers[V:steamID(),number] = 1
                Team1M:pushString(V:name())
                Team1Ent:pushEntity(V)
            }else{
                TeamMembers[V:steamID(),number] = 2
                Team2M:pushString(V:name())
                Team2Ent:pushEntity(V)
            }
            State = !State
        }
        inithud()
    }    
    ## Timer Setup
    function setupTimers(){
        
        # Main Loop
        timer("main",1)
        
    }
    
    ## Pickup
    function pickup(Ply:entity){
        
        if(ShowMarker){
            holoDelete(FlagMarker)
        }
        FlagHaver = Ply        
        holoCreate(1,Ply:toWorld(vec(0,0,164)),vec(1),ang(0,0,0),vec(255),FlagModel)
        holoAlpha(1,255)
        holoCreate(2,Ply:toWorld(vec(0,0,164))+vec(0,0,333),vec(1,1,1),ang(0,0,0),FlagColor,"models/effects/vol_light256x512.mdl")
        holoAlpha(2,255)
        if(Game == TDM){
            TeamHaver = TeamMembers[Ply:steamID(),number]
            holoColor(2,TeamColors[TeamHaver,vector])
        }
        holoEntity(1):soundPlay(1,0,PickSound)
        
    }
    
    ## Flag Point Selector
    function randPoint(){
        
        LastPoint = FlagPoint
        Index = randint(1,FlagPoints:count())
        NewPoint = FlagPoints[Index,vector]
        
        if(NewPoint == LastPoint || NewPoint == Goal){
            randPoint()
        }else{
            FlagPoint = NewPoint
            FlagTitle = FlagTitles[Index,string]
        }
        
    }
    
    ## Goal Point Selector
    function randGoal(){
        
        LastPoint = Goal
        Index = randint(1,FlagPoints:count())
        NewPoint = FlagPoints[Index,vector]
        
        if(NewPoint == LastPoint || NewPoint == FlagPoint){
            randGoal()
        }else{
            Goal = NewPoint
            GoalTitle = FlagTitles[Index,string]
            holoCreate(3,Goal+vec(0,0,64)+vec(0,0,333),vec(1,1,1),ang(0,0,0),GoalColor,"models/effects/vol_light256x512.mdl")
            holoAlpha(3,255)
        }
        
    }
    
    ## Minge Spawner
    function spawnMinge(PT:vector){
        
        holoCreate(1,PT,vec(1),ang(0,0,0),vec(255),FlagModel)
        holoAlpha(1,255)
        holoCreate(2,PT+vec(0,0,333),vec(1,1,1),ang(0,0,0),FlagColor,"models/effects/vol_light256x512.mdl")
        holoAlpha(2,255)
        
    }
    
    ## Minge Drop function
    function dropMinge(P:entity){
        
        holoCreate(1,P:pos()+vec(0,0,64),vec(1),ang(0,0,0),vec(255),FlagModel)
        holoAlpha(1,255)
        holoCreate(2,P:pos()+vec(0,0,64)+vec(0,0,333),vec(1,1,1),ang(0,0,0),FlagColor,"models/effects/vol_light256x512.mdl")
        holoAlpha(2,255)
        holoEntity(1):soundPlay(2,0,DropSound)
        if(ShowFlagMarker){
            holoCreate(FlagMarker,holoEntity(1):pos()+vec(0,0,64)+vec(0,0,333),vec(11),ang(0,0,0),FlagColor,"hq_sphere")
            holoMaterial(FlagMarker,"hlmv/debugmrmwireframe")
        }
        TeamHaver = 1337
        concmd("say "+"[Runics CTM] The Minge has been dropped!")
        
    }
        
    ## Parse Flag Points from Console
    function parseFlagPoints(){
        
        foreach(K,V:string = RawFlagPoints){
            Point = V:replace(" ",","):explode(",")
            FlagPoints:pushVector(vec(Point[1,string]:toNumber(),Point[2,string]:toNumber(),Point[3,string]:toNumber())-vec(0,0,64))
            printColor(vec(155,255,0),"[Runics CTM] Parsed Flag Point: "+V+" | Title: "+FlagTitles[K,string])
        }
        
    }
    
    ## New Points
    function new(){
        
        # Get New Point and Goal
        randPoint()
        randGoal()
        spawnMinge(FlagPoint)

        # If Score is added, change message
        if(FlagScoreAdd){
            
            # Score Free for All
            if(Game == FFA){
                concmd("say "+"[Runics CTM] "+FlagHaver:name()+" put a Minge back! | Score: "+ Score[FlagHaver:steamID(),number] +" | Origin is: ["+FlagTitle+"] | Goal is: ["+GoalTitle+"]")
            # Score Team Deathmatch
            }else{          
                concmd("say "+"[Runics CTM] The "+TeamNames[TeamHaver,string]+" put a Minge back! | Score: "+ Score[FlagHaver:steamID(),number] +" | Origin is: ["+FlagTitle+"] | Goal is: ["+GoalTitle+"]")    
            }
            
            # Reset FlagScore Trigger
            FlagScoreAdd = 0
        }else{
            if(FirstRun){
                if(Game == FFA){
                    concmd("say "+"[Runics CTM] NEW ROUND STARTED! | Score Limit: "+ScoreLimit+" | Gamemode is: FFA | Origin is: ["+FlagTitle+"] | Goal is: ["+GoalTitle+"]")
                }else{
                    concmd("say "+"[Runics CTM] NEW ROUND STARTED! | Score Limit: "+ScoreLimit+" | Gamemode is: Team CTF | Origin is: ["+FlagTitle+"] | Goal is: ["+GoalTitle+"]")
                }
                FirstRun = 0
            }else{
                concmd("say "+"[Runics CTM] Origin is: ["+FlagTitle+"] | Goal is: ["+GoalTitle+"]")
            }
        }
        
        # Reset Flag Haver
        FlagHaver = entity()
        LastFlagHaver = entity()
        
        # Markers, if Enabled.
        if(ShowMarker){
            holoCreate(Marker,Goal+vec(0,0,64)+vec(0,0,333),vec(11),ang(0,0,0),GoalColor,"hq_sphere")
            holoMaterial(Marker,"hlmv/debugmrmwireframe")
        }
        if(ShowFlagMarker){
            holoCreate(FlagMarker,FlagPoint+vec(0,0,64)+vec(0,0,333),vec(11),ang(0,0,0),FlagColor,"hq_sphere")
            holoMaterial(FlagMarker,"hlmv/debugmrmwireframe")
        }        
    }
    
    ## Score is added to player
    function addscore(Ply:entity){
        FlagScoreAdd = 1
        NewScore = (Score[Ply:steamID(),number] + 1)
        Score[Ply:steamID(),number] = NewScore
        holoCreate(55)
        holoPos(55,Goal)
        holoAlpha(55,0)
        holoEntity(55):soundPlay(1,0,WinSound)
        if(Score[Ply:steamID(),number] >= ScoreLimit){
            concmd("say "+"[Runics CTM] "+Ply:name()+" wins the game! Well memed.")
            selfDestruct()
        }else{
            new()
        }
    }
    
    function addscoreteam(Team:number){
        FlagScoreAdd = 1
        NewScore = (TeamScore[Team,number] + 1)
        TeamScore[Team,number] = NewScore
        holoCreate(55)
        holoPos(55,Goal)
        holoAlpha(55,0)
        holoEntity(55):soundPlay(1,0,WinSound)
        if(TeamScore[Team,number] >= ScoreLimit){
            concmd("say "+"[Runics CTM] "+TeamNames[Team,number]+" wins the game! Well memed.")
            selfDestruct()
        }else{
            new()
        }
    }

    ## Gamemode Setup
    function setupGamemode(){
        if(Game == FFA){ 
            foreach(K,V:entity = players()){
                Score[V:steamID(),number] = 0
            }            
        }else{
        
            # Balancer
            
            TeamScore[Team1,number] = 0
            TeamScore[Team2,number] = 0
            balance()
            timer("PrintTeam1",1500)
            timer("PrintTeam2",2500)
            
            # Hud init
            inithud()
        }
    }
    ## Init Function
    function init(){
        setupGamemode()
        parseFlagPoints()
        setupTimers()
        new()
    }

    
    # Call Init function     
    init()
    
}

# Main Loop
if(clk("main")){
    
    # 3D HUD Loops
    foreach(K:number,V:entity = Team1HUD){
        Index = K+40
        TargPos = V:pos()+vec(0,0,V:height()*0.5)
        holoPos(Index,TargPos)
    }
    foreach(K:number,V:entity = Team2HUD){
        Index = K+120
        TargPos = V:pos()+vec(0,0,V:height()*0.5)
        holoPos(Index,TargPos)
    }
    
    # Pick up loop
    foreach(K,V:entity = players()){
        if(V:pos():distance(holoEntity(1):pos()) < FlagRange && !FlagHaver:isPlayer() && V != LastFlagHaver){
            if(Game == FFA){
                concmd("say "+"[Runics CTM] "+V:name()+" picked up the Minge!")
            }else{
                concmd("say "+"[Runics CTM] "+V:name()+" of the ["+TeamNames[TeamMembers[V:steamID(),number],string]+"] picked up the Minge!")
            }
            pickup(V)
        }
    }
    
    # Goal loop
    if(FlagHaver:isPlayer()){
        
        holoPos(1,FlagHaver:toWorld(vec(0,0,164)))
        holoPos(2,FlagHaver:toWorld(vec(0,0,164+333)))
        if(FlagHaver:pos():distance(Goal) < FlagRange && Game == FFA){
            addscore(FlagHaver)
        }
        if(FlagHaver:pos():distance(Goal) < FlagRange && Game == TDM){
            addscoreteam(TeamHaver)
        }
    }
    if(perf()){
        PERFER = 60
    }else{
        PERFER = 200
    }
    timer("main",PERFER)
        
}

# If FlagHaver dies, drop the Minge for someone else to take
if(deathClk()){
    if(lastDeathVictim() == FlagHaver && FlagHaver:isPlayer()){
            LastFlagHaver = FlagHaver
            dropMinge(FlagHaver)
            FlagHaver = entity()

    }
}

# If the Person that dropped the minge spawns, he can pick it up again
if(spawnClk()){
    if(lastSpawnedPlayer() == LastFlagHaver && LastFlagHaver:isPlayer()){
            LastFlagHaver = entity()
    }
}
if(changed(players():count()) && Game == TDM){
    concmd("say [Runics CTM] Rebalancing...")
    rebalance()
    timer("PrintTeam1",1500)
    timer("PrintTeam2",2500)
}

# Print Team if Gamemode == TDM
if(clk("PrintTeam1")){    
    concmd("say ["+Team1+"]: "+Team1M:concat(" "))
}
if(clk("PrintTeam2")){
    concmd("say ["+Team2+"]: "+Team2M:concat(" "))
}

# Commands
if(chatClk(owner())){
    if(lastSaid() == "~rebalance"){
        concmd("say [Runics CTM] Rebalancing...")
        rebalance()
        timer("PrintTeam1",1500)
        timer("PrintTeam2",2500)    
    }
    if(lastSaid() == "~drop"){
        concmd("say [Runics CTM] Reset the Flag...")
        LastFlagHaver = FlagHaver
        FlagHaver = entity()
        dropMinge(LastFlagHaver)
    }
}
